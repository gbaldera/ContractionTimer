apply plugin: 'com.android.application'

repositories {
    jcenter()
    maven {
        url "http://JRAF.org/static/maven/2"
    }
}

android {
    buildToolsVersion "20.0.0"

    productFlavors {
        android {
            def versionMajor = 2
            def versionMinor = 0
            def versionPatch = 0
            def versionBuild = 0
            def betaVersion = 2

            compileSdkVersion 20
            minSdkVersion 9
            targetSdkVersion 20
            versionCode versionMajor * 10000000 + versionMinor * 100000 + versionPatch * 1000 + versionBuild
            versionName "${versionMajor}.${versionMinor}" +
                    (versionPatch > 0 ? ".${versionPatch}" : "") +
                    (betaVersion > 0 ? ", Beta ${betaVersion}" : "")

            def filesAuthorityValue = "com.ianhanniballake.contractiontimer.files"
            manifestPlaceholders = [filesAuthority: filesAuthorityValue]
            buildConfigField "String", "FILES_AUTHORITY", "\"${filesAuthorityValue}\""
        }

        glass {
            def versionMajor = 1
            def versionMinor = 0
            def versionPatch = 0
            def versionBuild = 0

            compileSdkVersion "Google Inc.:Glass Development Kit Preview:19"
            minSdkVersion 19
            targetSdkVersion 19
            versionCode versionMajor * 10000000 + versionMinor * 100000 + versionPatch * 1000 + versionBuild
            versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        }
    }

    signingConfigs {
        releaseConfig {
            storeFile System.getenv("KEYSTORE") != null ? file(System.getenv("KEYSTORE")) : null
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("CONTRACTION_TIMER_ALIAS")
            keyPassword System.getenv("CONTRACTION_TIMER_PASSWORD")
        }
    }

    buildTypes {
        release {
            runProguard true
            proguardFile getDefaultProguardFile('proguard-android.txt')
            proguardFile 'proguard-project.txt'
            signingConfig signingConfigs.releaseConfig
        }
    }
}

dependencies {
    androidCompile "com.android.support:appcompat-v7:20.0.+"
    androidCompile "com.android.support:gridlayout-v7:20.0.+"
    androidCompile "com.google.android.gms:play-services:4.4.52"
    androidCompile "ch.acra:acra:4.4.0"
    androidCompile "org.jraf:android-switch-backport:1.3.1"
    androidCompile "net.sf.opencsv:opencsv:2.3"
}