apply plugin: 'com.android.application'

repositories {
    jcenter()
    maven { url 'http://JRAF.org/static/maven/2' }
}

android {
    compileSdkVersion 20
    buildToolsVersion '20.0.0'

    defaultConfig {
        def versionMajor = 2
        def versionMinor = 0
        def versionPatch = 1
        def versionBuild = 1
        def betaVersion = 0

        minSdkVersion 9
        targetSdkVersion 20
        versionCode versionMajor * 10000000 + versionMinor * 100000 + versionPatch * 1000 + versionBuild
        versionName "${versionMajor}.${versionMinor}" +
                (versionPatch > 0 ? ".${versionPatch}" : "") +
                (betaVersion > 0 ? ", Beta ${betaVersion}" : "")

        def filesAuthorityValue = "com.ianhanniballake.contractiontimer.files"
        manifestPlaceholders = [filesAuthority: filesAuthorityValue]
        buildConfigField "String", "FILES_AUTHORITY", "\"${filesAuthorityValue}\""
    }

    signingConfigs {
        releaseConfig {
            storeFile System.getenv("KEYSTORE") != null ? file(System.getenv("KEYSTORE")) : null
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("CONTRACTION_TIMER_ALIAS")
            keyPassword System.getenv("CONTRACTION_TIMER_PASSWORD")
        }
    }

    buildTypes {
        release {
            runProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseConfig
        }
    }
}

dependencies {
    compile "com.android.support:appcompat-v7:20.0.+"
    compile "com.android.support:gridlayout-v7:20.0.+"
    compile "com.google.android.gms:play-services:5.0.77"
    compile "ch.acra:acra:4.4.0"
    compile "org.jraf:android-switch-backport:1.3.1"
    compile "net.sf.opencsv:opencsv:2.3"
    compile project(':provider')
}