apply plugin: 'com.android.application'

repositories {
    jcenter()
}

android {
    compileSdkVersion 21
    buildToolsVersion '21.0.2'

    defaultConfig {
        def versionMajor = 2
        def versionMinor = 0
        def versionPatch = 2
        def versionBuild = 2
        def betaVersion = 0

        minSdkVersion 9
        targetSdkVersion 21
        versionCode versionMajor * 10000000 + versionMinor * 100000 + versionPatch * 1000 + versionBuild
        versionName "${versionMajor}.${versionMinor}" +
                (versionPatch > 0 ? ".${versionPatch}" : "") +
                (betaVersion > 0 ? ", Beta ${betaVersion}" : "")

        def filesAuthorityValue = "com.ianhanniballake.contractiontimer.files"
        manifestPlaceholders = [filesAuthority: filesAuthorityValue]
        buildConfigField "String", "FILES_AUTHORITY", "\"${filesAuthorityValue}\""
    }

    signingConfigs {
        release {
            storeFile System.getenv("KEYSTORE") != null ? file(System.getenv("KEYSTORE")) : null
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("CONTRACTION_TIMER_ALIAS")
            keyPassword System.getenv("CONTRACTION_TIMER_PASSWORD")
        }
    }

    buildTypes {
        release {
            runProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    compile 'com.android.support:support-annotations:21.0.0'
    compile "com.android.support:appcompat-v7:21.0.0"
    compile "com.android.support:gridlayout-v7:21.0.0"
    compile "com.google.android.gms:play-services:6.1.11"
    compile "ch.acra:acra:4.5.0"
    compile "net.sf.opencsv:opencsv:2.3"
    compile project(':provider')
}